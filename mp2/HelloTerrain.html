<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>Hello Terrain </title>
<meta charset="utf-8"> 
</head>

<script id="shader-vs" type="x-shader/x-vertex">

    //Material diffuse colors
    const vec3 diff_snow = vec3(1.0, 0.980, 0.980);
    const vec3 diff_rock = vec3(0.353, 0.302, 0.255);
    const vec3 diff_earth = vec3(0.451, 0.271, 0.137);
    const vec3 diff_grass = vec3(0.376, 0.502, 0.220);
    const vec3 diff_sand = vec3(0.761, 0.698, 0.502);
    const vec3 diff_shallow_sea = vec3(0.231, 0.886, 0.800);
    const vec3 diff_deep_sea = vec3(0.157, 0.314, 0.835);
    
    //Terrain Divisions
    const float frac_snow = 0.92;
    const float frac_rock = 0.57;
    const float frac_earth = 0.35;
    const float frac_grass = -0.32;
    const float frac_sand = -0.45;
    const float frac_shallow_sea = -0.60;
    //Deep sea goes from -1.0 up to shallow sea value
    
    
    //Corner height used for setting up material colors
    //depending on the height of the current vertex
    uniform float cornerHeight;
    
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;    

    varying vec3 normalInterp;
    varying vec3 vertPos;
    
    //Color to be set to the vertex depending on its height
    varying vec3 uKDiffuse;
    
    void main(){
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vec4 vertPos4 = uMVMatrix * vec4(aVertexPosition, 1.0);
        vertPos = vec3(vertPos4) / vertPos4.w;
        normalInterp = uNMatrix * aVertexNormal;
        
        //Set color constants depending on vertex height
        if(aVertexPosition.z > cornerHeight*frac_snow)
            uKDiffuse = diff_snow;
        else if(aVertexPosition.z > cornerHeight*frac_rock)
            uKDiffuse = diff_rock;
        else if(aVertexPosition.z > cornerHeight*frac_earth)
            uKDiffuse = diff_earth;
        else if(aVertexPosition.z > cornerHeight*frac_grass)
            uKDiffuse = diff_grass;
        else if(aVertexPosition.z > cornerHeight*frac_sand)
            uKDiffuse = diff_sand;
        else if(aVertexPosition.z > cornerHeight*frac_shallow_sea)
            uKDiffuse = diff_shallow_sea;
        else
            uKDiffuse = diff_deep_sea;
    }
</script>
  
<script id="shader-fs" type="x-shader/x-fragment">

precision mediump float; 

varying vec3 normalInterp;
varying vec3 vertPos;
varying vec3 uKDiffuse;

uniform vec3 uLightPosition;
uniform vec3 uAmbientLightColor;
uniform vec3 uDiffuseLightColor;
uniform vec3 uSpecularLightColor;

uniform vec3 uKAmbient;
uniform vec3 uKSpecular;
uniform float uShininess;

void main() {

  vec3 normal = normalize(normalInterp);
  vec3 lightDir = normalize(uLightPosition - vertPos);
  
  float lambertian = max(dot(lightDir,normal), 0.0);
  float specular = 0.0;

  if(lambertian > 0.0) {

    vec3 viewDir = normalize(-vertPos);
    vec3 halfDir = normalize(lightDir + viewDir);
    float specAngle = max(dot(halfDir, normal), 0.0);
    specular = pow(specAngle, uShininess);
  }
  gl_FragColor = vec4(uAmbientLightColor * uKAmbient +
                        lambertian * uKDiffuse * uDiffuseLightColor +
                        specular * uKSpecular * uSpecularLightColor, 1.0);
}
</script>

<script src="gl-matrix-min.js"></script> 
<script src="webgl-utils.js"></script> 
<script src="Terrain.js"></script>  
<script src="HelloTerrain.js"></script>   
<body onload="startup();">
 <canvas id="myGLCanvas" width="900" height="500"></canvas>
</body>

</html>
